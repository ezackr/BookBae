apply plugin: 'java'
apply plugin: 'war'

war.archiveBaseName = "api"

repositories {
    mavenCentral()
}

sourceSets {
    intTest {}
    test {
        resources {
            srcDirs = [ "$projectDir/../database" ]
        }
    }
}

dependencies {
    providedCompile 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
    providedCompile 'jakarta.enterprise:jakarta.enterprise.cdi-api:3.0.1'
    providedCompile 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'com.fasterxml.jackson.jakarta.rs:jackson-jakarta-rs-json-provider:2.13.3'
    implementation 'org.apache.commons:commons-dbcp2:2.9.0'
    implementation 'com.microsoft.sqlserver:mssql-jdbc:7.2.2.jre11'
    implementation 'org.springframework.security:spring-security-crypto:5.6.2'
    implementation 'com.azure:azure-storage-blob:12.16.1'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    testImplementation 'com.h2database:h2:2.1.212'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
    testImplementation 'org.glassfish.jersey.core:jersey-server:3.1.0-M2'


    intTestImplementation 'org.glassfish.main.extras:glassfish-embedded-all:6.2.5'
    intTestImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    intTestImplementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.0.0'
    intTestImplementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
}

test {
    useJUnitPlatform()
}


tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    systemProperty "bookbae.archive",       "$buildDir/libs/api.war"
    systemProperty "bookbae.database_name", "BookBaeTestDB"
    systemProperty "bookbae.password",      project.findProperty('PASSWORD') ?: ''
    systemProperty "bookbae.server_url",    "bookbaeserver.database.windows.net"
    systemProperty "bookbae.username",      "TeamBookBae@bookbaeserver"
    shouldRunAfter test
    dependsOn build
    useJUnitPlatform()
}